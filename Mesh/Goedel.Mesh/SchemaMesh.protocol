Protocol Goedel.Mesh MeshItem MeshItem
	Description 
		|An entry in the Mesh linked logchain.

    Using Goedel.Cryptography.Jose
    Using Goedel.Cryptography.Dare
	Extern Key

	Section 2 "Shared Classes"
		Description
			|The following classes are used as common elements in
			|Mesh profile specifications.

	Section 3 "Classes describing keys"


	Structure KeyData
		Description
			|The KeyData class is used to describe public key pairs and 
			|trust assertions associated with a public key.
		String Udf
			Description
				|UDF fingerprint of the public key parameters
		Binary X509Certificate
			Description
				|List of X.509 Certificates
		Binary X509Chain
			Multiple
			Description
				|X.509 Certificate chain.
		Binary X509CSR
			Description
				|X.509 Certificate Signing Request.
		DateTime NotBefore
			Description
				|If present specifies a time instant that use of the private key
				|is not valid before.
		DateTime NotOnOrAfter
			Description
				|If present specifies a time instant that use of the private key
				|is not valid on or after.
		TStruct Key PublicParameters
			Description
				|The public key parameters as defined in the JOSE specification.
		TStruct Key PrivateParameters
			Description
				|The private key parameters as defined in the JOSE specification.
		
	
	Structure CompositePrivate
		Inherits Key
		String DeviceKeyUdf
			Description
				|UDF fingerprint of the bound device key (if used).
		TStruct Key PrivateSalt
			Description
				|Private parameters of additive key
		TStruct Key ServiceShare
			Description
				|Private parameters of serviced share



	Section 2 "Assertion classes"
		Description
			|Classes that are derived from an assertion.

	Structure Assertion
		Abstract
		Description 
			|Parent class from which all assertion classes are derived

		String Names
			Multiple
			Description
				|Fingerprints of index terms for profile retrieval. The use of the fingerprint
				|of the name rather than the name itself is a precaution against enumeration
				|attacks and other forms of abuse.
		DateTime Updated
			Description
				|The time instant the profile was last modified.
		String NotaryToken
			Description
				|A Uniform Notary Token providing evidence that a signature
				|was performed after the notary token was created.
		TStruct Condition Conditions
			Description
				|Conditional clause(s) that MAY be verified to evaluate the validity of the
				|assertion. At present no condition classes are specified.

	Structure Condition
		Abstract
		Description 
			|Parent class from which all condition classes are derived.

	Section 3 "Base Classes"
		Description
			|Abstract classes from which the Profile, Activation and Connection classes
			|are derrived.



	Structure Connection
		Inherits Assertion
		String SubjectUdf
			Description
				|UDF of the connection target.
		String AuthorityUdf
			Description
				|UDF of the connection source.

	Structure Activation
		Inherits Assertion
		Description 
			|Contains the private activation information for a Mesh application running on
			|a specific device
		String ActivationKey
			Description
				|Secret seed used to derive keys that are not explicitly specified.
		Struct ActivationEntry Entries
			Multiple
			Description
				|Activation of named resources.
	
	Structure ActivationEntry
		String Resource
			Description
				|Name of the activated resource
		Struct KeyData Key
			Description
				|The activation key or key share


	Section 3 "Mesh Profile Classes"
		Description
			|Classes describing Mesh Profiles. All Profiles are Assertions
			|derrived from Assertion.

	Structure Profile
		Abstract
		Inherits Assertion
		Description 
			|Parent class from which all profile classes are derived
		Struct KeyData ProfileSignature
			Description
				|The permanent signature key used to sign the profile itself. The UDF of
				|the key is used as the permanent object identifier of the profile. Thus,
				|by definition, the KeySignature value of a Profile does not change under
				|any circumstance.


	Structure ProfileDevice
		Inherits Profile
		Description 
			|Describes a mesh device.
		String Description
			Description
				|Description of the device
		Struct KeyData BaseEncryption
			Description
				|Base key contribution for encryption keys. 
				|Also used to decrypt activation data sent to the device
				|during connection to an account.
		Struct KeyData BaseAuthentication
			Description
				|Base key contribution for authentication keys. 
				|Also used to authenticate the device
				|during connection to an account.
		Struct KeyData BaseSignature
			Description
				|Base key contribution for signature keys. 

	Structure ProfileAccount
		Description
			|Base class for the account profiles ProfileUser and ProfileGroup.
			|These subclasses may be merged at some future date.
		Inherits Profile
		String AccountAddress
			Description
				|The account address. This is either a DNS service address 
				|(e.g. alice@example.com) or a Mesh Name (@alice).
		String ServiceUdf
			Description
				|The fingerprint of the service profile to which the account is
				|currently bound.
		Struct KeyData EscrowEncryption
			Description
				|Escrow key associated with the account.
		Struct KeyData AccountEncryption
			Description
				|Key currently used to encrypt data under this profile
		Struct KeyData AdministratorSignature
			Description
				|Key used to sign connection assertions to the account.

	Structure ProfileUser
		Inherits ProfileAccount
		Description 
			|Account assertion. This is signed by the service hosting the account.
		Struct KeyData AccountAuthentication
			Description
				|Key used to authenticate requests made under this user account.
		Struct KeyData AccountSignature
			Description
				|Key used to sign data under the account.

	Structure ProfileGroup
		Inherits ProfileAccount
		Description 
			|Describes a group. Note that while a group is created by one person who
			|becomes its first administrator, control of the group may pass to other
			|administrators over time.

	Structure ProfileService
		Inherits Profile
		Description 
			|Profile of a Mesh Service
		Struct KeyData ServiceAuthentication
			Description
				|Key used to authenticate service connections.
		Struct KeyData ServiceEncryption
			Description
				|Key used to encrypt data under this profile
		Struct KeyData ServiceSignature
			Description
				|Key used to sign data under the account.

	Structure ProfileHost
		Inherits Profile
		Struct KeyData KeyAuthentication
			Description
				|Key used to authenticate service connections.
		Struct KeyData KeyEncryption
			Description
				|Key used to pass encrypted data to the device such as a


	Section 3 "Connection Assertions"
		Description
			|Connection assertions are used to authenticate and authorize
			|interactions between devices and the service currently servicing
			|the account. They SHOULD NOT be visible to external parties.

	Structure ConnectionDevice
		Inherits Connection
		Description
			|Connection assertion used to authenticate service requests made
			|by a device.
		String AccountAddress
			Description
				|The account address
		Struct KeyData DeviceSignature
			Description
				|The signature key for use of the device under the profile
		Struct KeyData DeviceEncryption
			Description
				|The encryption key for use of the device under the profile
		Struct KeyData DeviceAuthentication
			Description
				|The authentication key for use of the device under the profile

	Structure ConnectionApplication
		Inherits Connection
		Description
			|Connection assertion stating that a particular device is 

	Structure ConnectionGroup
		Description
			|Describes the connection of a member to a group.
		Inherits Connection

	Structure ConnectionService
		Inherits Connection

	Structure ConnectionHost
		Inherits Connection

	Section 3 "Activation Assertions"
		Description
			|
	Structure ActivationDevice
		Description
			|Contains activation data for device specific keys used in the context of a 
			|Mesh account.
		Inherits Activation
		String AccountUdf
			Description
				|The UDF of the account


	Structure ActivationAccount
		Inherits Activation
		Struct KeyData ProfileSignature
			Description
				|Grant access to profile online signing key used to sign updates
				|to the profile.
		Struct KeyData AdministratorSignature
			Description
				|Grant access to Profile administration key used to make changes to
				|administrator catalogs.
		Struct KeyData AccountEncryption
			Description
				|Grant access to ProfileUser account encryption key
		Struct KeyData AccountAuthentication
			Description
				|Grant access to ProfileUser account authentication key
		Struct KeyData AccountSignature
			Description
				|Grant access to ProfileUser account signature key

	Structure ActivationApplication
		Inherits Activation


	Section 2 "Data Structures"
		Description
			|Classes describing data used in cataloged data.	

	Structure Contact
		Abstract
		Inherits Assertion
		Description
			|Base class for contact entries.
		String Id
			Description
				|The globally unique contact identifier.
		Struct Anchor Anchors
			Multiple
			Description
				|Mesh fingerprints associated with the contact.
		Struct NetworkAddress NetworkAddresses
			Multiple
			Description
				|Network address entries
		Struct Location Locations
			Multiple
			Description
				|The physical locations the contact is associated with.
		Struct Role Roles
			Multiple
			Description
				|The roles of the contact
		Struct Bookmark Bookmark 
			Multiple
			Description
				|The Web sites and other online presences of the contact
		Struct TaggedSource Sources
			Multiple
			Description
				|Source(s) from which this contact was constructed.

	Structure Anchor
		Description
			|Trust anchor
		String Udf
			Description
				|The trust anchor.
		String Validation
			Description
				|The means of validation.

	Structure TaggedSource
		Description
			|Source from which contact information was obtained.
		String LocalName
			Description
				|Short name for the contact information.
		String Validation
			Description
				|The means of validation.		
		Binary BinarySource
			Description
				|The contact data in binary form.
		Struct Enveloped<Contact> EnvelopedSource
			Description
				|The contact data in enveloped form. If present, the BinarySource property
				|is ignored.

	Structure ContactGroup
		Inherits Contact
		Description
			|Contact for a group, including encryption groups.

	Structure ContactPerson
		Inherits Contact
		Description
			|
		Struct PersonName CommonNames
			Multiple
			Description
				|List of person names in order of preference

	Structure ContactOrganization
		Inherits Contact
		Description
			|		
		Struct OrganizationName CommonNames
			Multiple
			Description
				|List of person names in order of preference

	Structure OrganizationName
		Description
			|The name of an organization
		Boolean Inactive
			Description
				|If true, the name is not in current use.
		String RegisteredName
			Description
				|The registered name.
		String DBA
			Description
				|Names that the organization uses including trading names
				|and doing business as names.


	Structure PersonName
		Description
			|The name of a natural person
		Boolean Inactive
			Description
				|If true, the name is not in current use.
		String FullName
			Description
				|The preferred presentation of the full name.
		String Prefix
			Description
				|Honorific or title, E.g. Sir, Lord, Dr., Mr.
		String First
			Description
				|First name.
		String Middle
			Multiple
			Description
				|Middle names or initials.
		String Last
			Description
				|Last name.
		String Suffix
			Description
				|Nominal suffix, e.g. Jr., III, etc.
		String PostNominal
			Description
				|Post nominal letters (if used).

	Structure NetworkAddress
		Description
			|Provides all means of contacting the individual according to a
			|particular network address
		Boolean Inactive
			Description
				|If true, the name is not in current use.
		String Address	
			Description
				|The network address, e.g. alice@example.com
		String NetworkCapability
			Multiple
			Description
				|The capabilities bound to this address.
		Struct Enveloped<ProfileAccount> EnvelopedProfileAccount
			Description
				|The account profile
		Struct NetworkProtocol Protocols
			Multiple
			Description
				|Public keys associated with the network address
		TStruct CryptographicCapability Capabilities
			Multiple
			Description
				|Cryptographic capabilities that may be claimed from this address

	Structure NetworkProtocol
		String Protocol
			Description
				|The IANA protocol|identifier of the network protocols by which 
				|the contact may be reached using the specified Address. 
		TStruct CryptographicCapability Capabilities
			Multiple
			Description
				|Cryptographic keys representing capabilities.


	Structure Role
		String OrganizationName
			Description
				|The organization at which the role is held
		String Titles
			Multiple
			Description
				|The titles held with respect to that organization.
		Struct Location Locations
			Multiple
			Description
				|Postal or physical addresses associated with the role.

	Structure Location
		String Appartment
		String Street
		String District
		String Locality
		String County
		String Postcode
		String Country
		Float Latitude
		Float Longitude

	Structure Bookmark
		String Uri
		String Title
		String Role
			Multiple

	Structure Reference
		String MessageId
			Description
				|The received message to which this is a response
		String ResponseId
			Description
				|Message that was generated in response to the original (optional).
		String Relationship
			Description
				|The relationship type. This can be Read, Unread, Accept, Reject.



	Structure Task
		String Key
			Description
				|Unique key.
		DateTime Start
		DateTime Finish
		String StartTravel
		String FinishTravel
		String TimeZone
		String Title
		String Description
		String Location
		String Trigger
			Multiple
		String Conference
			Multiple
		String Repeat
		Boolean Busy
	
	Section 2 "Catalog Entries"

	Structure CatalogedEntry
		Abstract
		Description
			|Base class for cataloged Mesh data.
		String Labels
			Multiple
			Description
				|The set of labels describing the entry

	Structure CatalogedDevice
		Inherits CatalogedEntry
		Description
			|Public device entry, indexed under the device ID Hello
		String Udf
			Description
				|UDF of the signature key of the device in the Mesh
		String DeviceUdf
			Description
				|UDF of the offline signature key of the device
		String SignatureUdf
			Description
				|UDF of the account online signature key
		Struct Enveloped<ProfileAccount> EnvelopedProfileUser
			Description
				|The Mesh profile
		Struct Enveloped<ProfileDevice> EnvelopedProfileDevice
			Description
				|The device profile
		Struct Enveloped<ConnectionDevice> EnvelopedConnectionUser
			Description
				|The public assertion demonstrating connection of the Device to the Mesh
		Struct Enveloped<ActivationDevice> EnvelopedActivationDevice
			Description
				|The activation of the device within the Mesh account
		Struct Enveloped<ActivationAccount> EnvelopedActivationAccount
			Description
				|The activation of the device within the Mesh account
		Struct Enveloped<ActivationApplication> EnvelopedActivationApplication
			Multiple
			Description
				|Application activations granted to the device.

	Structure CatalogedPublication
		Inherits CatalogedEntry
		Description
			|A publication.
		String Id
			Description
				|Unique identifier code
		String Authenticator
			Description
				|The witness key value to use to request access to the record.	
		Struct DareEnvelope EnvelopedData
			Description
				|Dare Envelope containing the entry data. The data type is specified
				|by the envelope metadata.
		DateTime NotOnOrAfter
			Description
				|Epiration time (inclusive)


	Structure CatalogedCredential
		Inherits CatalogedEntry
		String Protocol
		String Service
		String Username
		String Password
	
	Structure CatalogedNetwork
		Inherits CatalogedEntry
		String Protocol
		String Service
		String Username
		String Password

	Structure CatalogedContact
		Inherits CatalogedEntry
		String Key
			Description
				|Unique key. 
		Boolean Self
			Description
				|If true, this catalog entry is for the user who created the catalog.
		TStruct Contact Contact
			Description
				|The contact information as edited by the catalog owner.


	Structure CatalogedAccess
		Inherits CatalogedEntry
		Description
			|

		TStruct CryptographicCapability Capability
			Description
				|The cataloged capability.



	Structure CryptographicCapability
		Abstract

		String Id
			Description
				|The identifier of the capability. If this is a user capability, MUST match the
				|KeyData identifier. If this is a serviced capability, MUST match the value of
				|ServiceId on the corresponding service capability.
		Struct KeyData KeyData 
			Description
				|The key that enables the capability
		Struct Enveloped<KeyData> EnvelopedKeyShares
			Multiple
			Description
				|One or more enveloped key shares.
		String SubjectId
			Description
				|The identifier of the resource that is controlled using the key.
		String SubjectAddress
			Description
				|The address of the resource that is controlled using the key.

	Structure CapabilityDecrypt
		Inherits CryptographicCapability
		Description
			|The corresponding key is a decryption key

	Structure CapabilityDecryptPartial 
		Inherits CapabilityDecrypt
		Description
			|The corresponding key is an encryption key
		String ServiceId
			Description
				|The identifier used to claim the capability from the service.[Only present for
				|a partial capability.]
		String ServiceAddress
			Description
				|The service account that supports a serviced capability. [Only present for
				|a partial capability.]

	Structure CapabilityDecryptServiced
		Inherits CapabilityDecrypt
		Description
			|The corresponding key is an encryption key
		String AuthenticationId
			Description
				|UDF of trust root under which request to use a serviced capability must be 
				|authorized. [Only present for a serviced capability]


	Structure CapabilitySign
		Inherits CryptographicCapability
		Description
			|The corresponding key is an administration key


	Structure CapabilityKeyGenerate
		Inherits CryptographicCapability
		Description
			|The corresponding key is a key that may be used to generate key shares.


	Structure CapabilityFairExchange
		Inherits CryptographicCapability
		Description
			|The corresponding key is a decryption key to be used in accordance 
			|with the Micali Fair Electronic Exchange with Invisible Trusted Parties
			|protocol.




	Structure CatalogedBookmark
		Inherits CatalogedEntry
		String Uri
		String Title
		String Path


	Structure CatalogedTask
		Inherits CatalogedEntry
		Struct Enveloped<Task> EnvelopedTask
		// Calendar entry
		// Geolocation entry
		String Title
		String Key
			Description
				|Unique key.


		

	Structure CatalogedApplication
		// The application profile
		Inherits CatalogedEntry
		String Key
		Struct DareEnvelope EnvelopedCapabilities
			Multiple
			Description
				|Enveloped keys for use with Application


	Structure CatalogedMember
		String ContactAddress
		String MemberCapabilityId
		String ServiceCapabilityId
		Inherits CatalogedEntry

	Structure CatalogedGroup
		Inherits CatalogedApplication
		Struct Enveloped<ProfileAccount> EnvelopedProfileGroup
			Description
				|The Mesh profile
		Struct Enveloped<ActivationAccount> EnvelopedActivationAccount
			Description
				|The activation of the device within the Mesh account


	Structure CatalogedApplicationSSH
		Inherits CatalogedApplication
		// The recryption key
	Structure CatalogedApplicationMail
		Inherits CatalogedApplication
		// The recryption key
	Structure CatalogedApplicationNetwork
		Inherits CatalogedApplication
		// The recryption key


	Section 2 "Publications"

	Structure DevicePreconfiguration
		Description
			|A data structure that is passed 
		Struct Enveloped<ProfileDevice> EnvelopedProfileDevice
			Description
				|The device profile
		Struct Enveloped<ConnectionDevice> EnvelopedConnectionDevice
			Description
				|The device connection
		TStruct Key PrivateKey
			Description
				|The device private key
		String ConnectUri
			Description
				|The connection URI. This would normally be printed on the device as a 
				|QR code.


	Section 2 "Messages"

	Structure Message
		String MessageId
			Description
				|Unique per-message ID. When encapsulating a Mesh Message in a DARE envelope,
				|the envelope EnvelopeID field MUST be a UDF fingerprint of the MessageId
				|value. 
		String Sender
		String Recipient

	Structure MessageError
		Inherits Message
		String ErrorCode

	Structure MessageComplete
		Inherits Message
		Struct Reference References
			Multiple

	Structure MessagePinValidated
		Inherits Message
		Struct DareEnvelope AuthenticatedData
			Description
				|Enveloped data that is authenticated by means of the PIN
		Binary ClientNonce
			Description
				|Nonce provided by the client to validate the PIN
		String PinId
			Description
				|Pin identifier value calculated from the PIN code, action and account address.
		Binary PinWitness
			Description
				|Witness value calculated as KDF (Device.Udf + AccountAddress, ClientNonce)


	Structure MessagePin
		String Account

		Inherits Message
		DateTime Expires
		Boolean Automatic
			Description
				|If true, authentication against the PIN code is sufficient to complete
				|the associated action without further authorization.
		String SaltedPin
			Description
				|PIN code bound to the specified action.
		String Action
			Description
				|The action to which this PIN code is bound.
				
	Structure RequestConnection
		Description
			|Connection request message. This message contains the information
		Inherits MessagePinValidated
		String AccountAddress
			Description
				|

	Structure AcknowledgeConnection
		Description
			|Connection request message generated by a service on receipt of a valid
			|MessageConnectionRequestClient
		Inherits Message
		Struct Enveloped<RequestConnection> EnvelopedRequestConnection
			Description
				|The client connection request.
		Binary ServerNonce
			Description
				|
		String Witness
			Description
				|

	Structure RespondConnection
		Description
			|Respond to RequestConnection message to grant or refuse the connection
			|request.
		Inherits Message
		String Result 
			Description
				|The response to the request. One of "Accept", "Reject" or "Pending".
				
		Struct CatalogedDevice CatalogedDevice
			Description
				|The device information. MUST be present if the value of Result is
				|"Accept". MUST be absent or null otherwise.


	Structure MessageContact
		Inherits MessagePinValidated
		Boolean Reply
			Description
				|If true, requests that the recipient return their own contact information
				|in reply.
		String Subject
			Description
				|Optional explanation of the reason for the request.
		String PIN
			Description
				|One time authentication code supplied to a recipient to allow authentication
				|of the response.



	Structure GroupInvitation
		Inherits Message
		String Text
		TStruct Contact Contact
			Description
				|The contact data.




	Structure RequestConfirmation
		Inherits Message
		String Text


	Structure ResponseConfirmation
		Inherits Message
		Struct Enveloped<RequestConfirmation> Request
		Boolean Accept

	Structure RequestTask
		Inherits Message


	Structure MessageClaim
		Inherits Message
		String PublicationId
		String ServiceAuthenticate
		String DeviceAuthenticate

		DateTime Expires


	Structure ProcessResult
		Description
			|For future use, allows logging of operations and results	
		Inherits Message
		Boolean Success
		String ErrorReport
			Description
				|The error report code.

